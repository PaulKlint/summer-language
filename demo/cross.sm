#***************************************************************
*								*
*	Word intersections					*
*	See Griswold & Hanson					*
*	"An overview of the Icon programming language"		*
*	All intersections of common characters in two words	*
*	are printed.						*
****************************************************************#


proc cross(word1, word2)
( scan word1
  for while break(word2)
      do var j := cursor;
	 scan word2
         for while break(word1[j])
	     do
		xprint(word1, word2, j, cursor);
		move(1)
	     od
	 rof;
	 move(1)
      od
  rof
);

proc xprint(word1, word2, j, k)
( var i;

  for i in word2.index 
  do
     if i = k then
        put(word1, '\n')
     else
	put(word2[i].right(j + 1, ' ') , '\n')
     fi
  od;
  put('\n')
);

program crosses ()
(  var data, line;

  data := file('cross.data','r');
  while line := data.get 
  do
     scan line
     for
	 cross(break(':'), move(1) & rtab(0))
     rof
  od
)
