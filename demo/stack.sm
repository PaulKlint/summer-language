class stack(n)
begin fetch push, pop, print;
  var sp, space;
  proc push(x)
  ( if sp = n then freturn
    else
       space[sp] := x;
       sp := sp + 1;
       return(x)
    fi
  );
  proc pop()
  ( if sp = 0 then freturn
    else
       sp := sp - 1;
       return(space[sp])
    fi
  );
  proc print()
  ( var i;
    for i in interval(0, sp - 1, 1)
    do put(i, ':\t', space[i], '\n') od
  );
init:
  sp := 0;
  space := array(n, undefined);
end stack;

class random_access_stack(n)
begin subclass of stack;
      fetch access;

      proc access(i)
      ( if i >= 0 & i < sp
        then
           return(space[i])
        else
           freturn
        fi
      );
end random_access_stack;

program stacks ()
(var s1, s2, i;
  s1 := stack(10);
  s2 := stack(20);
  i := 0;
  while s1.push(i)
  do s2.push(i); s2.push(2 * i); i := i + 1 od;

  s1.print; s2.print;
  s1 := random_access_stack(5);
  s1.push(11); s1.push(12);
  put(s1.access(0), s1.access(1));
)
