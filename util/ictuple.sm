#********************************************************************
*									*
* ictuples -- count frequency of intermediate code instruction tuples	*
*									*
************************************************************************#

proc ic_op(data)
( const letter := 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_';
  return( scan data.get for span(letter) rof )
);
program ictuple(files)
( var f, tuple, tupletab := table(1000, 0);

  for f in files
  do var data;
     if data := file(f,'r')fails then
        put('Can''t open ', f, '\n')
     else
	var op1 , op2 := ic_op(data), op3 := ic_op(data);

        while [op1, op2, op3] := [op2, op3, ic_op(data)]
	do
	   var pair := op1 || '/' || op2, triple := pair || '/' || op3;
	   tupletab[pair] := tupletab[pair] + 1;
	   tupletab[triple] := tupletab[triple] + 1;
	od;
	data.close 
     fi
  od;

  for tuple in tupletab.index 
  do var freq := tupletab[tuple];
     if freq > 3 then
        put(tuple.left(20, ' ') , string(freq).right(10, ' ') , '\n')
     fi
  od
)
