
#************************************************************************
*									*
*		N-queens problem					*
*									*
************************************************************************#

var N := 8,	# number of rows/columns #
    a := array(N+1, -1),		# rows #
    b := array(2*(N+1), -1),	# left to right diagonals #
    c := array(2*(N+1), -1);	# right to left diagonals #

proc attacked(row, col)
   if a[row] ~= -1 | b[row+col] ~= -1 | c[row-col+N] ~= -1 then
      return
   else var i := 1;
        while i <= N do if a[i] = col then return fi; i := i + 1 od;
        freturn
   fi;

proc place_queen(row, col)
   a[row] := b[row+col] := c[row-col+N] := col;

proc print_bord()
( var col := 1;
  put('(');
  while col <= N do put(a[col], if col = N then ')\n' else ',' fi); col:= col+1 od;
);

proc queens(row)
( if row = N+1 then
     print_bord();
     return
  else
     var col := 1;
     while col <= N
     do
        if ~attacked(row, col) then
           if try place_queen(row, col) & queens(row+1) yrt
	   then else fi
        fi;
        col := col + 1
     od;
     freturn
  fi
);

program Nqueens ()
(  queens(1) | ''
);
