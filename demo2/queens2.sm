
#************************************************************************
*									*
*		N-queens problem					*
*									*
************************************************************************#

var N := 8,	# number of rows/columns #
    a := array(N+1, -1),		# rows #
    b := array(2*(N+1), -1),	# left to right diagonals #
    c := array(2*(N+1), -1);	# right to left diagonals #

proc attacked(row, col)
   if a[row] ~= -1 | b[row+col] ~= -1 | c[row-col+N] ~= -1 then
      return
   else var i := 1;
        while i <= N do if a[i] = col then return fi; i := i + 1 od;
        freturn
   fi;

proc place_queen(row, col)
   a[row] := b[row+col] := c[row-col+N] := col;
proc remove_queen(row, col)
  if 1 then
     a[row] := b[row+col] := c[row-col+N] := -1;
  fi;

proc print_bord()
( var col := 1;
  put('(');
  while col <= N do put(a[col], if col = N then ')\n' else ',' fi); col:= col+1 od;
);

class queen(col)
begin fetch advance;

	var row := 0;

	proc advance ()
	( remove_queen(row, col);
	  if row < 8 then
	     row := row + 1;
	     if attacked(row, col) then
		return(self.advance)
	     else
	        place_queen(row, col);
		return('')
	     fi
	  else
	     row := 0;
	     freturn
	  fi;
	);
end queen;

program Nqueens ()
( var i, q := array(N+1, 0);

  for i in interval(1, 8, 1) do q[i] := queen(i) od;
  i := 1;
  while i ~= 0
  do
     if q[i].advance then
        if i = 8 then
           print_bord()
	else
	   i := i + 1
        fi
     else
        i := i - 1
     fi
  od
)
