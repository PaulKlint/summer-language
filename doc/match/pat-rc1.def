    proc match(curs, pat)
    (  var curs1, curs2, P, P1, Q, Q1, pats, val;
       var str, expr, id, windowvars, SAVED_ENV;
       enter([curs, pat]);
#a#    if {{ pat == P:<pattern-primary> '|' Q:<pattern-primary> }}
       then
          if curs1 := match(curs, P)
          then
             return(?curs1)
          elif curs2 := match(curs, Q)
          then
             return(?curs2)
          else
             fret;freturn
          fi
#b#    elif {{ pat == P:<pattern-primary> '--' Q:<pattern-primary> }}
       then
          if curs1 := match(curs, P)
          then
             if curs2 := match(curs1, Q)
             then
                return(?curs2)
             else
                fret;freturn
             fi
          fi;
          fret;freturn
#c#    elif {{ pat == '(' P:<pattern> ')' }}
       then
          return(?match(curs, P))
#d#    elif {{ pat == str:<string-literal> }}
       then
          if curs1 := litmatch(curs, str)
          then
             return(?curs1)
          else
             fret;freturn
          fi
#e#    elif {{ pat == id:<identifier> }}
       then
          if curs1 := match(curs, INITIAL_ENV.binding(id))
          then
             return(?curs1)
          else
             fret;freturn
          fi
#f#    elif {{ pat == '*' id:<identifier> }}
       then
          if curs1 := match(curs, CURRENT_ENV.binding(id))
          then
             return(?curs1)
          else
             fret;freturn
          fi
#g#    elif {{ pat == SBAS '(' P:<pattern-primary> ',' id:<identifier> ')' }}
       then
          if curs1 := match(curs, P)
          then
             CURRENT_ENV.bind(id, mk_string(curs, curs1));
             return(?curs1)
          else
             fret;freturn
          fi
#h#    elif {{ pat == ACT '(' expr:<expression> ')' }}
       then
          eval(expr);
          return(?curs)
#i#    elif {{ pat == TRY '<' windowvars: { <identifier> ',' }* '>'
                      pats:{<pattern> ','}+
                      UNTIL Q: <pattern> YRT }}
       then
          SAVED_ENV := copy(CURRENT_ENV);
          for P in pats
          do if curs1 := match(curs, P)
             then
                if curs2 := match(curs1, Q)
                then
                   return(?curs2)
                fi
             fi;
             for id in windowvars
             do SAVED_ENV.bind(id, CURRENT_ENV.binding(id)) od;
             CURRENT_ENV := copy(SAVED_ENV);
          od;
          fret;freturn
#j#    else
          ERROR
       fi
    );
