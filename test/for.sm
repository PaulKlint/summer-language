var x,
    a10 := (x := array(10,0);
	    for i in range(0,9)
	    do  x[i] := i od;
	    x);

var i;


class range(low,up)
begin	fetch next;

	var step := if low < up then 1 else -1 fi;

	proc next(x)
	(	if x = undefined
		then	return([low, low+step])
		elif step > 0 & x > up | step < 0 & x < up
		then	freturn
		else	return([x, x+step])
		fi
	);
end range;


class list(inits)
begin	fetch info, nextelem, next, print;
	store info, nextelem;

	var info, nextelem;

	proc next(x)
	(	if x = undefined
		then	return([info, nextelem])
		elif x.info = undefined & x.nextelem = undefined
		then	freturn
		else	return([x.info, x.nextelem])
		fi
	);

        proc print (first)
        ( if first = 0 then put('[') fi;
          if info = undefined then put(']\n'); return fi;
          if first ~= 0 then put(', ') fi;
          put(info);
          nextelem.print(1)
        );
	var i;

	init:	var ll;

		if inits.size = 0
		then	info := nextelem := undefined
		else	ll := self;
			for i in range(0, inits.size -1)
			do	ll.info := inits[i];
				ll.nextelem := list([]);
				ll := ll.nextelem
			od
		fi
end list;

program forloops()
(	var j, ll, x, y;

	for i in range(1,10)
	do	put(i, ' ')	od;
	put('\n');
	for j in range(10,1)
# following line corrected #
	do	put(j, ' ')	od;
	put('\n');
	ll := list(['a', 'dd', 'g', 'nil', 'pi']);
	ll.print(0);
	for x in ll
	do	for y in ll
		do	put(x.left(3,' '), ',', y.left(5, ' ') ) od;
			put('\n')
	od;
);
#
1 2 3 4 5 6 7 8 9 10
10 9 8 7 6 5 4 3 2 1
a  ,a    a  ,dd   a  ,g    a  ,nil  a  ,pi
dd ,a    dd ,dd   dd ,g    dd ,nil  dd ,pi
g  ,a    g  ,dd   g  ,g    g  ,nil  g  ,pi
nil,a    nil,dd   nil,g    nil,nil  nil,pi
pi ,a    pi ,dd   pi ,g    pi ,nil  pi ,pi
#

