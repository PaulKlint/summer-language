#						#
# tbits -- bit manipulation			#
#						#

include 'heading.sm';

program tbits()
( var a, b, c, i, j, x, n;

  start('bits');
  test(n := 0);
  a := bits(1, 0);
  if type(a) ~= 'bits' then error(n+0) fi;
  if a[0] ~= 0 then error(n+1) fi;
  a[0] := 1;
  if a[0] ~= 1 then error(n+2) fi;

  test(n := 10);
  a := bits(16, 1);
  i := 0;
  while i < 16
  do if a[i] ~= 1 then error(n+i) fi;
     i := i + 1
  od;

  test(n := 20);
  a := bits(17, 0);
  if a[16] ~= 0 then error(n+0) fi;
  a[16] := 1;
  if a[16] ~= 1 then error(n+1) fi;

  a[15] := 1;
  if a[15] ~= 1 then error(n+2) fi;

  test(n := 30);
  a := bits(201, 0);
  i := 0;
  while i < 201 do
     a[i] := 1;
     j := 0;
     while j < 201 do
       if a[j] ~= if i = j then 1 else 0 fi then error(n+i) fi;
       j := j + 1
     od;
     a[i] := 0;
     i := i + 1
  od;

  test(n := 40);
  a := bits(23, 0);
  b := bits(27, 1);
  c := a.disj(b) ;
  x := c.size ;
  if x ~= 27 then error(n-1) fi;
  i := 0;
  while i < x do
    if c[i] ~= 1 then error(n+i) fi;
    i := i + 1
  od;

  test(n := 50);
  a := bits(37, 0);
  i := 0;
  while i < 37 do a[i] := 1; i := i + 2 od;
  b := a.compl ;
  i := 0;
  while i < 36 do
     if b[i] ~= 0 | b[i+1] ~= 1 then error(n+i) fi;
     i := i + 2
  od;

  test(n := 60);
  c := a.conj(b) ;
  i := 0;
  while i < 37 do
     if c[i] ~= 0 then error(n+i) fi;
     i := i + 1
  od;

  test(n := 70);
  c := a.disj(b) ;
  i := 0;
  while i < 37 do
     if c[i] ~= 1 then error(n+i) fi;
     i := i + 1
  od;

  finish('bits');
)
